/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */

configure(childProjects.values()) {

  apply plugin: 'base'

  def baseProject = project
  def baseName = "elasticsearch-${it.name}"
  base {
    archivesName = baseName
  }
  subprojects {
    plugins.withType(JavaPlugin).whenPluginAdded {
      def name = baseName
      def currentProject = project
      while (currentProject != baseProject) {
        name += "-${currentProject.name}"
        currentProject = currentProject.parent
      }
      base {
        archivesName = name
      }
    }
  }

  if (project.name == 'log4j') {
    return
  }

  /*
   * All subprojects are java projects using Elasticsearch's standard build
   * tools.
   */
  apply plugin: 'elasticsearch.build'
}
