/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */

import static java.util.stream.Collectors.joining

apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.embedded-providers'

embeddedProviders {
  impl 'entitlement-agent', project(':distribution:tools:entitlement-agent:impl')
}

configurations {
  entitlementBridge
}

dependencies {
  entitlementBridge project(":distribution:tools:entitlement-bridge")
  compileOnly project(":libs:elasticsearch-core")
  compileOnly project(":distribution:tools:entitlement-runtime")
  compileOnly project(":distribution:tools:entitlement-bridge")
  testImplementation project(":test:framework")
  testImplementation project(":distribution:tools:entitlement-agent:impl")
}

tasks.named('test').configure {
  systemProperty "tests.security.manager", "false"
  dependsOn('jar')
  jvmArgs "-javaagent:${ tasks.named('jar').flatMap{ it.archiveFile }.get()}"

  def bridgeFiles = configurations.entitlementBridge.files
  def bridgeJars = bridgeFiles.stream().map(File::toString).collect(joining(File.pathSeparator))
  jvmArgs "-Des.entitlements.bridgeJars=$bridgeJars"


  // The Elasticsearch build plugin automatically adds all compileOnly deps as testImplementation.
  // We must not add the bridge this way because it is also on the boot classpath, and that would lead to jar hell.
  classpath -= files(bridgeFiles)
}

tasks.named('jar').configure {
  manifest {
    attributes(
      'Premain-Class': 'org.elasticsearch.entitlement.agent.EntitlementAgent'
      , 'Can-Retransform-Classes': 'true'
    )
  }
}

tasks.named('forbiddenApisMain').configure {
  replaceSignatureFiles 'jdk-signatures'
}

